// Shortcut for creating Android modules with consistent dependencies and configuration
// Not every module needs all these dependencies, but I'm lazy :shrug"
ext.androidModule = { Boolean isApplication = false ->
    if(isApplication) {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'kotlin-parcelize'
    apply plugin: 'kotlinx-serialization'
    apply plugin: 'dagger.hilt.android.plugin'

    android {
        compileSdkVersion 30
        buildToolsVersion "30.0.2"

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 30
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildFeatures {
            viewBinding true
        }

        buildTypes {
            release {
                minifyEnabled false
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
            freeCompilerArgs += [
                    '-module-name', project.path.replace(":", "_"),
                    '-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi',
                    '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
            ]
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
        implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$versions.kotlinSerializationJson"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"

        implementation "androidx.appcompat:appcompat:$versions.appcompat"
        implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"
        implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
        implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
        implementation "androidx.fragment:fragment-ktx:$versions.fragment"
        implementation "androidx.swiperefreshlayout:swiperefreshlayout:$versions.swipeRefreshLayout"

        implementation "com.google.android.material:material:$versions.material"

        implementation "nav.enro:enro:$versions.enro"
        kapt "nav.enro:enro-processor:$versions.enro"

        implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
        implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:$versions.retrofitKotlinSerialization"


        implementation "com.google.dagger:hilt-android:$versions.hilt"
        kapt "com.google.dagger:hilt-android-compiler:$versions.hilt"
        implementation "androidx.hilt:hilt-lifecycle-viewmodel:$versions.hiltAndroid"
        kapt "androidx.hilt:hilt-compiler:$versions.hiltAndroid"

        testImplementation "androidx.test.ext:junit:$versions.junit_android"
        testImplementation "junit:junit:$versions.junit"
        testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$versions.coroutines"
        testImplementation "io.mockk:mockk:$versions.mockk"
        testImplementation "androidx.test.ext:junit:$versions.junitAndroid"
        testImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
        testImplementation "androidx.test:runner:$versions.testRunner"

        androidTestImplementation "androidx.test.ext:junit:$versions.junitAndroid"
        androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
        androidTestImplementation "androidx.test:runner:$versions.testRunner"
    }
}

ext.versions = [
        kotlin: "1.4.20",
        kotlinSerializationJson: "1.0.1",
        coroutines: "1.4.2",

        retrofit: "2.9.0",
        retrofitKotlinSerialization: "0.8.0",

        enro: "1.2.6",

        hilt: "2.30.1-alpha",
        hiltAndroid: "1.0.0-alpha02",

        appcompat: "1.2.0",
        constraintlayout: "2.0.4",
        lifecycle: "2.2.0",
        material: "1.2.1",
        fragment: "1.2.5",
        swipeRefreshLayout: "1.1.0",

        mockk: "1.10.3-jdk8",
        junit: "4.13.1",
        junitAndroid: "1.1.2",
        testRunner: "1.3.0",
        espresso: "3.3.0"
]